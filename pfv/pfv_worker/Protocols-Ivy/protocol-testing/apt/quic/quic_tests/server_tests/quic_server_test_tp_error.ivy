#lang ivy1.7

include order
include quic_infer
include file
include ivy_quic_shim_client
include quic_locale
include ivy_quic_client_behavior
#
# We fix the initial transport parameters here. TODO: In principle these
# parameters should be selected randomly by some event.
#

action make_transport_parameters returns (tp:quic_transport_parameters) = {
    var imsdbl : initial_max_stream_data_bidi_local;
    imsdbl.stream_pos_32 := random_stream_pos(1,0xFFFFFFF);
    var imd : initial_max_data;
    imd.stream_pos_32 := random_stream_pos(1,0xFFFFFFF);
    var it : max_idle_timeout;
    it.seconds_16 := random_microsecs(0,0xFFFFFFF);
    var imsdbr : initial_max_stream_data_bidi_remote;
    imsdbr.stream_pos_32 := random_stream_pos(1,0xFFFFFFF);
    var imsdu : initial_max_stream_data_uni;
    imsdu.stream_pos_32 := random_stream_pos(1,0xFFFFFFF);
    tp.transport_parameters := tp.transport_parameters.append(imsdbl);
    tp.transport_parameters := tp.transport_parameters.append(imd);
    tp.transport_parameters := tp.transport_parameters.append(it);
    tp.transport_parameters := tp.transport_parameters.append(imsdbr);
    tp.transport_parameters := tp.transport_parameters.append(imsdu);

    var icid : initial_source_connection_id; #TODO update v29
    icid.scid := the_cid;
    tp.transport_parameters := tp.transport_parameters.append(icid);

    var max_streams_bidi : initial_max_stream_id_bidi; #TODO update v29
    max_streams_bidi.stream_id_16 := random_stream_id(4,0xFFFFFFF);
    tp.transport_parameters := tp.transport_parameters.append(max_streams_bidi);

    #ack_delay_exponent (0x0a):  The acknowledgement delay exponent is an
    #  integer value indicating an exponent used to decode the ACK Delay
    #  field in the ACK quic_frame (Section 19.3).  If this value is absent, a
    #  default value of 3 is assumed (indicating a multiplier of 8).
    #  Values above 20 are invalid.

    var acl_del : ack_delay_exponent;
    acl_del.exponent_8 := greater_microsecs(21);
    require acl_del.exponent_8 > 20; # useless generating now working here
    tp.transport_parameters := tp.transport_parameters.append(acl_del);
}

after init {
    sock := net.open(endpoint_id.client,client.ep);
    sock_alt := net.open(endpoint_id.client_alt,client_alt);
    sock_vn := sock; # net.open(endpoint_id.client_vn,client_vn);
    client.set_tls_id(0);
    second_client.set_tls_id(0); # TODO to avoid error
    server.set_tls_id(1);
    var extns := tls_extensions.empty;
    extns := extns.append(make_transport_parameters);
    call tls_api.upper.create(0,false,extns);  # false means this instance of tls is not a server
}


# Generate connection_close frames only for the environment process(es).
#
# Note: requiring the `err_code` is zero on non-generated frames means that
# we stop the test if the peer reports a protocol error. 

before quic_frame.connection_close.handle(f:quic_frame.connection_close,scid:cid,dcid:cid) {
    if _generating {
        require scid = the_cid;
    } else {
        require is_transport_parameter_error
    }
}

# Generate application_close frames only for the environment process(es).

before quic_frame.application_close.handle(f:quic_frame.application_close,scid:cid,dcid:cid) {
    if _generating {
        require scid = the_cid;
    }
}


# The actions listed below will be generated by the mirror.
#
# Note: some of these are commented out. They are added in other files
# that include this one.

export quic_frame.ack.handle
export quic_frame.stream.handle
export quic_frame.crypto.handle
export quic_frame.path_response.handle
export packet_event
export client_send_event
export tls_recv_event
#export quic_frame.new_connection_id.handle
#export quic_frame.retire_connection_id.handle
#export quic_frame.rst_stream.handle
#export quic_frame.max_stream_id.handle
#export quic_frame.connection_close.handle
#export quic_frame.max_stream_data.handle

# Final check
#
# When the test is complete, the tester calls the special action `_finalize`.
# We use this action to make some heuristic checks, for example that some
# data was actually received from the server. We can add advice to this
# action with additional checks.

export action _finalize = {
    # chris TODO 
    require is_transport_parameter_error;
}


